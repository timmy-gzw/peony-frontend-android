apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'
apply plugin: 'com.alibaba.arouter'
apply plugin: 'privacy-sentry-plugin'
apply plugin: 'android-junk-code'

def name = 'OFFICIAL'
def appName = "伊糖"

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.tftechsz.peony"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode version_code as int
        versionName version_name
        flavorDimensions "versionCode"
        vectorDrawables.useSupportLibrary = true
        renderscriptTargetApi 25
        renderscriptSupportModeEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: name,
                                APPLOG_SCHEME      : "rangersapplog.__TBD__".toLowerCase(),
                                app_label          : appName,
                                app_icon           : "@mipmap/ic_launcher",
        ]

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
        resConfigs("zh", "zh-rCN")
    }
    signingConfigs {
        peony {
            storeFile file('peony.jks')
            storePassword "cikJayHiow"
            keyAlias "peony"
            keyPassword "Pixiznuf"
        }
        emerald {
            storeFile file('emerald.jks')
            storePassword "cikJayHiow"
            keyAlias "emerald"
            keyPassword "cikJayHiow"
        }
    }
    buildTypes {

        debug {
            signingConfig null //null 走flavor配置
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [app_icon: "@mipmap/ic_launcher"]
        }

        release {
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def input = ""
                    if (variant.productFlavors[0].name == "office")
                        input = "Peony"
                    else if (variant.productFlavors[0].name == "heart")
                        input = "ivy"
                    else if (variant.productFlavors[0].name == "jasmine")
                        input = "jasmine"
                    else if (variant.productFlavors[0].name == "poinsettia")
                        input = "poinsettia"
                    outputFileName = input + "_V${productFlavors[0].versionName}_${productFlavors[0].versionCode}.apk"
                }
                variant.mergeAssetsProvider.configure {
                    doLast {
                        delete(fileTree(dir: outputDir,
                                includes: ['model/ai_face_processor.bundle',
                                           'model/ai_gesture.bundle',
                                           'model/ai_hand_processor.bundle',
                                           'model/ai_human_processor.bundle',
                                           'graphics/controller.bundle',
                                           'graphics/fuzzytoonfilter.bundle',
                                           'graphics/fxaa.bundle',
                                           'graphics/face_makeup.bundle',
                                           'graphics/tongue.bundle']))
                    }
                }
            }
        }

    }

    packagingOptions {
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        //pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
    }

    useLibrary 'org.apache.http.legacy'

    buildFeatures {
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
    }
    flavorDimensions rootProject.ext.dimension
    productFlavors {
        office {
            dimension 'office'
            applicationId "com.tftechsz.peony"
            versionCode rootProject.ext.appVersion.versionCode
            versionName rootProject.ext.appVersion.versionName
            signingConfig signingConfigs.peony
            manifestPlaceholders = rootProject.ext.manifestPlaceholders
        }
        heart {
            dimension 'office'
            applicationId "com.tftechsz.ivy"
            versionCode rootProject.ext.heartAppVersion.versionCode
            versionName rootProject.ext.heartAppVersion.versionName
            signingConfig signingConfigs.peony
            manifestPlaceholders = rootProject.ext.heartPlaceholders
        }
        jasmine {
            dimension 'office'
            applicationId "com.tftechsz.jasmine"
            versionCode rootProject.ext.jasmineAppVersion.versionCode
            versionName rootProject.ext.jasmineAppVersion.versionName
            signingConfig signingConfigs.peony
            manifestPlaceholders = rootProject.ext.jasminePlaceholders
        }
        poinsettia {
            dimension 'office'
            applicationId "com.emeraldtechhn.poinsettia"
            versionCode rootProject.ext.poinsettiaAppVersion.versionCode
            versionName rootProject.ext.poinsettiaAppVersion.versionName
            signingConfig signingConfigs.emerald
            manifestPlaceholders = rootProject.ext.poinsettiaPlaceholders
        }
    }
}

androidJunkCode {
    def heartConfig = {
        packageBase = "com.tftechsz.ivy.atest"
        packageCount = 60
        activityCountPerPackage = 8
        excludeActivityJavaFile = false
        otherCountPerPackage = 80
        methodCountPerClass = 60
        resPrefix = "ivy"
        drawableCount = 600
        stringCount = 600
    }
    def jasmineConfig = {
        packageBase = "com.tftechsz.jasmine.atest"
        packageCount = 60
        activityCountPerPackage = 8
        excludeActivityJavaFile = false
        otherCountPerPackage = 80
        methodCountPerClass = 60
        resPrefix = "jasmine"
        drawableCount = 600
        stringCount = 600
    }
    def poinsettiaConfig = {
        packageBase = "com.emeraldtechhn.poinsettia.atest"
        packageCount = 100
        activityCountPerPackage = 50
        excludeActivityJavaFile = false
        otherCountPerPackage = 100
        methodCountPerClass = 70
        resPrefix = "poinsettia"
        drawableCount = 800
        stringCount = 800
    }
    variantConfig {
        "heartRelease" heartConfig
        "jasmineRelease" jasmineConfig
        "poinsettiaRelease" poinsettiaConfig
    }
}

repositories {
    flatDir {
        dirs '../app/libs'
    }
}


configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

// 黑名单配置，可以设置这部分包名不会被修改字节码
// 项目里如果有引入高德地图，先加黑 blackList = ["com.loc","com.amap.api"], asm的版本有冲突
// 如果需要生成静态扫描文件， 默认名是replace.json
privacy {
    blackList = []
    replaceFileName = "replace.json"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    if (isModule.toBoolean()) {
        implementation project(':main')
        implementation project(':home')
        implementation project(':moment')
        implementation project(':im')
        implementation project(':mine')
        implementation project(':videotrimmer')
    }

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
//    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

}
